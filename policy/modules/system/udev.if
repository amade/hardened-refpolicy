## <summary>Policy for udev.</summary>

########################################
## <summary>
##	Send generic signals to udev.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_signal',`
	gen_require(`
		type udev_t;
	')

	allow $1 udev_t:process signal;
')

########################################
## <summary>
##	Execute udev in the udev domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`udev_domtrans',`
	gen_require(`
		type udev_t, udev_exec_t;
	')

	domtrans_pattern($1, udev_exec_t, udev_t)
')

########################################
## <summary>
##	Execute udev in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_exec',`
	gen_require(`
		type udev_exec_t;
	')

	can_exec($1, udev_exec_t)
')

########################################
## <summary>
##	Execute a udev helper in the udev domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`udev_helper_domtrans',`
	gen_require(`
		type udev_t, udev_helper_exec_t;
	')

	domtrans_pattern($1, udev_helper_exec_t, udev_t)
')

########################################
## <summary>
##	Allow process to read udev process state.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_read_state',`
	gen_require(`
		type udev_t;
	')

	kernel_search_proc($1)
	allow $1 udev_t:file read_file_perms;
	allow $1 udev_t:lnk_file read_lnk_file_perms;
')

########################################
## <summary>
##	Do not audit attempts to inherit a
##	udev file descriptor.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`udev_dontaudit_use_fds',`
	gen_require(`
		type udev_t;
	')

	dontaudit $1 udev_t:fd use;
')

########################################
## <summary>
##	Do not audit attempts to read or write
##	to a udev unix datagram socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`udev_dontaudit_rw_dgram_sockets',`
	gen_require(`
		type udev_t;
	')

	dontaudit $1 udev_t:unix_dgram_socket { read write };
')

########################################
## <summary>
##	Read udev rules files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_read_rules_files',`
	gen_require(`
		type udev_rules_t;
	')

	files_search_etc($1) # /etc/udev/rules.d
	udev_search_pids($1) # /run/udev/rules.d
	read_files_pattern($1, udev_rules_t, udev_rules_t)
')


########################################
## <summary>
##	Manage udev rules files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_manage_rules_files',`
	gen_require(`
		type udev_rules_t;
	')

	manage_files_pattern($1, udev_rules_t, udev_rules_t)

	files_search_etc($1)

	udev_search_pids($1)
')

########################################
## <summary>
##	Do not audit search of udev database directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`udev_dontaudit_search_db',`
	gen_require(`
		type udev_tbl_t;
	')

	dontaudit $1 udev_tbl_t:dir search_dir_perms;
')

########################################
## <summary>
##	Read the udev device table.
## </summary>
## <desc>
##	<p>
##	Allow the specified domain to read the udev device table.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <infoflow type="read" weight="10"/>
#
interface(`udev_read_db',`
	gen_require(`
		type udev_tbl_t;
	')

	allow $1 udev_tbl_t:dir list_dir_perms;

	read_files_pattern($1, udev_tbl_t, udev_tbl_t)
	read_lnk_files_pattern($1, udev_tbl_t, udev_tbl_t)

	dev_list_all_dev_nodes($1)

	files_search_etc($1)

	# Device table files are beneith /run/udev
	udev_search_pids($1)
')

########################################
## <summary>
##	Allow process to modify list of devices.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_rw_db',`
	gen_require(`
		type udev_tbl_t;
	')

	dev_list_all_dev_nodes($1)
	allow $1 udev_tbl_t:file rw_file_perms;
')

########################################
## <summary>
##	Create udev database directories
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_create_db_dirs',`
	gen_require(`
		type udev_tbl_t;
		type udev_var_run_t;
	')

	create_dirs_pattern($1, udev_var_run_t, udev_tbl_t)
')



########################################
## <summary>
##	Write in /var/run/udev with the udev_tbl_t (udev database) file type
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="class">
## 	<summary>
##	Classes on which the file transition should occur
##	</summary>
## </param>
## <param name="name" optional="true">
##	<summary>
##	Name of the directory that the file transition will work on
##	</summary>
## </param>
#
interface(`udev_pid_filetrans_db',`
	gen_require(`
		type udev_tbl_t;
		type udev_var_run_t;
	')

	filetrans_pattern($1, udev_var_run_t, udev_tbl_t, $2, $3)
')

########################################
## <summary>
## 	Search through udev pid content
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_search_pids',`
	gen_require(`
		type udev_var_run_t;
	')

	files_search_var_lib($1)
	search_dirs_pattern($1, udev_var_run_t, udev_var_run_t)
')

########################################
## <summary>
##	Create, read, write, and delete
##	udev run directories
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_manage_pid_dirs',`
	gen_require(`
		type udev_var_run_t;
	')

	files_search_var_lib($1)
	manage_dirs_pattern($1, udev_var_run_t, udev_var_run_t)
')

########################################
## <summary>
##	Read udev pid files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_read_pid_files',`
	gen_require(`
		type udev_var_run_t;
	')

	files_search_pids($1)
	read_files_pattern($1, udev_var_run_t, udev_var_run_t)
')


########################################
## <summary>
##	Create, read, write, and delete
##	udev pid files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_manage_pid_files',`
	gen_require(`
		type udev_var_run_t;
	')

	files_search_pids($1)
	manage_files_pattern($1, udev_var_run_t, udev_var_run_t)
')

########################################
## <summary>
##	Write dirs in /var/run with the udev_var_run file type.
##	This method is deprecated in favor of the init_daemon_run_dir call.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="name" optional="true">
##	<summary>
##	Name of the directory that the file transition will work on
##	</summary>
## </param>
#
interface(`udev_generic_pid_filetrans_run_dirs',`
	refpolicywarn(`$0($*) has been deprecated.')
')

#########################################
## <summary>
##	Write in /var/run/udev with the udev_rules_t (udev rules) file type
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="class">
## 	<summary>
##	Classes on which the file transition should occur
##	</summary>
## </param>
## <param name="name" optional="true">
##	<summary>
##	Name of the directory that the file transition will work on
##	</summary>
## </param>
#
interface(`udev_pid_filetrans_rules',`
	gen_require(`
		type udev_rules_t;
		type udev_var_run_t;
	')

	filetrans_pattern($1, udev_var_run_t, udev_rules_t, $2, $3)
')

########################################
## <summary>
##	Create udev rules directories
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`udev_create_rules_dirs',`
	gen_require(`
		type udev_rules_t;
		type udev_var_run_t;
	')

	create_dirs_pattern($1, udev_var_run_t, udev_rules_t)
')

